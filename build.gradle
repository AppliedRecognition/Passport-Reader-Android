// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        google()
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
        maven {
            url 'https://maven.google.com/'
            name 'Google'
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.4.1'
        classpath 'com.netflix.nebula:gradle-lint-plugin:10.4.2'
        classpath 'org.jfrog.buildinfo:build-info-extractor-gradle:4.8.1'
        classpath 'io.fabric.tools:gradle:1.27.0'
        classpath 'com.getkeepsafe.dexcount:dexcount-gradle-plugin:0.8.4'
        classpath "com.vanniktech:gradle-android-apk-size-plugin:0.4.0"
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    apply plugin: 'maven'
    apply plugin: "maven-publish"
    apply plugin: 'nebula.lint'
    apply plugin: 'com.jfrog.artifactory'
    repositories {
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
        maven {
            url 'https://maven.google.com/'
            name 'Google'
        }
        maven {
            url 'https://dev.ver-id.com/artifactory/gradle-release'
            name 'Ver-ID'
        }
        google()
    }
    // gradleLint.rules = ['all-dependency']
}

artifactory {
    contextUrl = verid_artifactory_url
    //The base Artifactory URL if not overridden by the publisher/resolver
    publish {
        repository {
            repoKey = verid_artifactory_repo
            username = verid_artifactory_username
            password = verid_artifactory_password
            maven = true
        }
        defaults {
            publications('mrtdreader')
            publishArtifacts = true
        }
    }
}

ext {
    version = "1.0.1"
    jmrtdVersion = "0.5.6"
    certcvcVersion = "1.4.3"
    appcompatv7Version = "28.0.0"
    constraintlayoutVersion = "1.1.3"
    scubaVersion = "0.0.9"
    spongycastleVersion = "1.54.0.0"
    multidexVersion = "1.0.3"
    gsonVersion = "2.8.5"
}

project('mrtdreader') {
    artifactoryPublish.dependsOn('assembleRelease')
    publishing {
        publications {
            mrtdreader(MavenPublication) {
                groupId "com.appliedrec"
                version rootProject.ext.version
                artifactId "mrtd-reader"
                artifact("$buildDir/outputs/aar/mrtd-reader-release.aar") {
                    extension "aar"
                }
                pom.withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')
                    def jmrtdNode = dependenciesNode.appendNode('dependency')
                    jmrtdNode.appendNode('groupId', 'org.jmrtd')
                    jmrtdNode.appendNode('artifactId', 'jmrtd')
                    jmrtdNode.appendNode('version', rootProject.jmrtdVersion)
                    def certCvcNode = dependenciesNode.appendNode('dependency')
                    certCvcNode.appendNode('groupId', 'org.ejbca.cvc')
                    certCvcNode.appendNode('artifactId', 'cert-cvc')
                    certCvcNode.appendNode('version', rootProject.certcvcVersion)
                    def androidSupportNode = dependenciesNode.appendNode('dependency')
                    androidSupportNode.appendNode('groupId', 'com.android.support')
                    androidSupportNode.appendNode('artifactId', 'appcompat-v7')
                    androidSupportNode.appendNode('version', rootProject.appcompatv7Version)
                    def constraintLayoutNode = dependenciesNode.appendNode('dependency')
                    constraintLayoutNode.appendNode('groupId', 'com.android.support.constraint')
                    constraintLayoutNode.appendNode('artifactId', 'constraint-layout')
                    constraintLayoutNode.appendNode('version', rootProject.constraintlayoutVersion)
                    def scubaNode = dependenciesNode.appendNode('dependency')
                    scubaNode.appendNode('groupId', 'net.sf.scuba')
                    scubaNode.appendNode('artifactId', 'scuba-sc-android')
                    scubaNode.appendNode('version', rootProject.scubaVersion)
                    def spongyCastleNode = dependenciesNode.appendNode('dependency')
                    spongyCastleNode.appendNode('groupId', 'com.madgag.spongycastle')
                    spongyCastleNode.appendNode('artifactId', 'prov')
                    spongyCastleNode.appendNode('version', rootProject.spongycastleVersion)
                }
            }
        }
    }

    artifactoryPublish {
        publications(publishing.publications.mrtdreader)
    }
}